import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

static String getKeystorePath() {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) return "c:/Keystore/civilcam"
    if (Os.isFamily(Os.FAMILY_MAC)) return "${System.properties['user.home']}/Keystore/civilcam"
    return System.getenv("CIVILCAM_KEYSTORE_PATH")
}

def computeVersionName() {
    return "${rootProject.version_major}.${rootProject.version_minor}.${rootProject.version_patch}"
}

def computeVersionCode() {
    return rootProject.version_major * 10000 + rootProject.version_minor * 1000 + rootProject.version_patch * 100 + rootProject.build_number
}

android {
    setProperty("archivesBaseName", getArtifactName(defaultConfig))

    compileSdk rootProject.compile_sdk

    defaultConfig {
        applicationId "com.civilcam"
        minSdk rootProject.min_sdk
        targetSdk rootProject.target_sdk
        versionCode computeVersionCode()
        versionName computeVersionName()

        buildConfigField "String", "GLEAP_KEY", '"6mCvq0I9X4vV8tascP6NIhmYKWztnr4u"'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "API_GATEWAY", '"https://api.staging.civilcam.cloud/api"'
        buildConfigField "String", "BASE_URL", '"https://api.staging.civilcam.cloud"'
        buildConfigField "String", "API_KEY", '"c&c753"'
    }

    signingConfigs {
        debug_config {
            try {
                keyAlias KEY_ALIAS
                keyPassword KEY_PASSWORD
                storeFile file(getKeystorePath())
                storePassword KEYSTORE_PASSWORD
            } catch (ignored) {
                throw new InvalidUserDataException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }
        }
        default_config {
            try {
                keyAlias KEY_ALIAS
                keyPassword KEY_PASSWORD
                storeFile file(getKeystorePath())
                storePassword KEYSTORE_PASSWORD
            } catch (ignored) {
                throw new InvalidUserDataException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }
        }
    }



    buildTypes {
        sandbox {
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.default_config
        }

        production {
            minifyEnabled true
            debuggable false
            signingConfig signingConfigs.default_config
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
    buildFeatures {
        viewBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.3.0-rc02"
    }
}

apply from: '../dependencies.gradle'

dependencies {
    implementation 'androidx.core:core-animation:1.0.0-alpha02'
    implementation 'androidx.camera:camera-view:1.0.0-alpha26'
    implementation 'androidx.camera:camera-core:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidX()

//    testImplementation 'junit:junit:4.13.2'
//    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
//    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    jetpackCompose()
    google()
    retrofit()
    okhttp()
    paging()
    koin()
    nav()
    adapterDelegates()
    glide()
    rxJava()
    compression()
    gleap()
    datePicker()
    camera()
    firebase()
    //Timber
    implementation 'com.jakewharton.timber:timber:5.0.1'

    api project(":app:arch")
    implementation project(path: ':domainLayer')

}